apply plugin: 'com.android.application'
//配套配置信息
ext {
    //应用名称
    moduleName = "VoiceControl_unisound"
    //应用包名
    modulePackageName = "com.kuyou.vc"
    //混淆
    isConfuse = rootProject.ext.isConfuse
}

android {
    signingConfigs {
        release {
            storeFile file("${rootProject.ext.KeyStoreFllePath}")
            storePassword KeyStorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "${project.modulePackageName}"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "${versionNumberCustom()}"
        ndk {
            abiFilters "armeabi-v7a"
        }
        //混淆开关
        buildConfigField "boolean", "IS_ENABLE_CONFUSE", "Boolean.parseBoolean(\"${project.isConfuse}\")"
        resValue "string", "app_name", "${project.moduleName}"
    }
    sourceSets{
        main{
            //jni libs path set
            jniLibs.srcDirs = ['libs'];
        }
    }

    buildTypes {
        release {
            if (project.isConfuse) {
                zipAlignEnabled project.isConfuse
                minifyEnabled project.isConfuse
                shrinkResources project.isConfuse
            }else{
                minifyEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release //加入签名配置
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${rootProject.ext.projectHardwareName}_${defaultConfig.versionName}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
     packagingOptions {
        pickFirst 'lib/arm64-v8a/libserial_port.so'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compileOnly files('compileOnlyLibs/framework.jar')

    implementation files('libs/usc.jar')

    implementation project(path: ':lib_common_ku09')

    implementation 'androidx.appcompat:appcompat:1.2.0'
}

//added by wgx
def versionNumberCustom() {
    def buildType = "debug"
    if (isReleaseBuildType()) {
        buildType = "release"
    }
    return "${project.moduleName}_" + "${buildType}_" + rootProject.ext.versionDate;
}

boolean isReleaseBuildType(){
    for(String s : gradle.startParameter.taskNames) {
        if (s.contains("Release") | s.contains("release")) {
            return true
        }
    }
    return false
}

task aInstallReleaseLaunch(type: Exec){
    dependsOn 'installRelease'
    commandLine  "adb","shell","am","start","-n","${project.modulePackageName}/.MainActivity"
}
