plugins {
    id 'com.android.application'
}
//配套配置信息
ext {
    //应用名称
    moduleName = "AudioVideoCommunication_peergine"
    //应用包名
    modulePackageName = "com.kuyou.avc"
    //混淆
    isConfuse = rootProject.ext.isConfuse
}

android {
    signingConfigs {
        release {
            storeFile file("${rootProject.ext.KeyStoreFllePath}")
            storePassword KeyStorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "${project.modulePackageName}"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1

        versionName "${versionNumberCustom()}"

        ndk {
            abiFilters "armeabi-v7a"
        }
        //混淆开关
        buildConfigField "boolean", "IS_ENABLE_CONFUSE", "Boolean.parseBoolean(\"${project.isConfuse}\")"
        resValue "string", "app_name", "${project.moduleName}"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            if (project.isConfuse) {
                zipAlignEnabled project.isConfuse
                minifyEnabled project.isConfuse
                shrinkResources project.isConfuse
            }else{
                minifyEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release //加入签名配置
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${rootProject.ext.projectHardwareName}_${defaultConfig.versionName}.apk"
            def applicationId = [variant.mergedFlavor.applicationId, variant.buildType.applicationIdSuffix].findAll().join()
        }
    }
}

dependencies {
    compileOnly files('compileOnlyLibs/framework.jar')

    implementation files('libs/pgnpp.jar')
    implementation files('libs/pgliblivemulti.jar')
    implementation files('implementationLibs/seek_android_sdk_4.1.2.0.aar')

    implementation project(path: ':lib_common_ku09')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

//added by wgx
def versionNumberCustom() {
    def buildType = "debug"
    if (isReleaseBuildType()) {
        buildType = "release"
    }
    return "${project.moduleName}_" + "${buildType}_" + rootProject.ext.versionDate;
}

boolean isReleaseBuildType() {
    for (String s : gradle.startParameter.taskNames) {
        if (s.contains("Release") | s.contains("release")) {
            return true
        }
    }
    return false
}

task aInstallReleaseLaunch(type: Exec){
    dependsOn 'installRelease'
    commandLine  "adb","shell","am","start","-n","${project.modulePackageName}/.MainActivity"
}
